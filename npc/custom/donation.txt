//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
prontera,140,180,5	script	Donation Manager#1	833,{ 
mes "[Donation Manager]";
mes "Permanent Conversion Rate: 100 Token of appreciation =  55 PHP or 1 USD";
mes "-----------------------------";
mes "2500 Token of Appreciation is valued around 1375PHP or 25 USD.";
mes "-----------------------------";
mes "2500 Token of Appreciation is equivalent to 25 Proof of donation.";
mes "-----------------------------";
mes "To get 25 proof Donation you will need to donate around 1375PHP or 25 USD.";
mes "-----------------------------";
mes "Any Excess PHP or USD will be rounded off";
callfunc "donateshop"; 

OnInit:
set .message$, "Donation NPC.......";

while (1) {
	set .message$, delchar(.message$+charat(.message$,0),0);
	delwaitingroom;
	waitingroom .message$, 0;
	sleep 200;
}
end;


}
// prontera,165,203,6	script	Quest Shop#2	998,{ callfunc "donateshop",1,2; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	donate_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,1;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Purchase Gift Packages",	// Shop Named 1
		"Proof of Donation Shop",		// Shop Named 2
		"Bonus Points Shop";		// Shop Named 3

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

// Shop 1
//remove all official quest for the items below
	Add(1,50049,1,0,0,6358,250);
	Add(1,50050,1,0,0,6358,100);
	Add(1,50017,1,0,0,6358,25);
	Add(1,3150,1,0,0,6358,2500);
	Add(1,3151,1,0,0,6358,2500);
	Add(1,3152,1,0,0,6358,2500);
	Add(1,3153,1,0,0,6358,2500);
	Add(1,3154,1,0,0,6358,3000);
	Add(1,3155,1,0,0,6358,3500);
	Add(1,3156,1,0,0,6358,3500);
	Add(1,3157,1,0,0,6358,4500);
// Shop 2
	Add(2,50051,1,0,0,7179,5);
	Add(2,50052,1,0,0,7179,5);
	Add(2,12412,1,0,0,7179,1);
	Add(2,6232,1,0,0,7179,5);
	Add(2,6228,1,0,0,7179,5);
	Add(2,3505,1,0,0,7179,10);
	Add(2,50035,1,0,0,7179,15);
	Add(2,50033,1,0,0,7179,15);
	Add(2,50034,1,0,0,7179,15);
	Add(2,50037,1,0,0,7179,20);
	Add(2,50036,1,0,0,7179,25);
	Add(2,50027,1,0,0,7179,15);
	Add(2,3018,1,0,0,7179,25);
	Add(2,3017,1,0,0,7179,25);
	Add(2,5151,1,0,0,7179,30);
	Add(2,5135,1,0,0,7179,30);
	Add(2,50004,1,0,0,7179,50);
	Add(2,3808,1,0,0,7179,70);
	Add(2,3823,1,0,0,7179,75);

// Shop 3 // March items
//	Add(3,5387,1,0,0,7558,30);
//	Add(3,5288,1,0,0,7558,30);
//	Add(3,5317,1,0,0,7558,40);
//	Add(3,5402,1,0,0,7558,40);
//	Add(3,5377,1,0,0,7558,50);
//	Add(3,5597,1,0,0,7558,50);
	//Add(3,5657,1,0,0,7558,50);
//	Add(3,5395,1,0,0,7558,60);


// Shop 3 // May items
//	Add(3,20103,1,0,0,7558,50);
//	Add(3,20376,1,0,0,7558,60);
//	Add(3,20451,1,0,0,7558,60);
//	Add(3,31160,1,0,0,7558,70);
//	Add(3,18656,1,0,0,7558,100);
//	Add(3,20315,1,0,0,7558,125);
//	Add(3,20325,1,0,0,7558,150);
//	Add(3,20404,1,0,0,7558,150);

//shop 3 //June Items
//	Add(3,18528,1,0,0,7558,50);
//	Add(3,18546,1,0,0,7558,70);
//	Add(3,18740,1,0,0,7558,100);
//	Add(3,20398,1,0,0,7558,150);
//	Add(3,20255,1,0,0,7558,150);
//	Add(3,19776,1,0,0,7558,175);
//	Add(3,18895,1,0,0,7558,175);
//	Add(3,31165,1,0,0,7558,175);

//shop 3 //July Items
	Add(3,5426,1,0,0,7558,80);
	Add(3,5751,1,0,0,7558,125);
	Add(3,20011,1,0,0,7558,140);
	Add(3,20440,1,0,0,7558,150);
	Add(3,20183,1,0,0,7558,175);
	Add(3,19910,1,0,0,7558,180);
	Add(3,31057,1,0,0,7558,200);


// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "donateshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "donateshop"+@shop_index,1;
	npcshopattach "donateshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		//switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000: ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has Purchased "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+" from the Donation NPC!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "donateshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	donateshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "donate_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	donateshop1	-1,909:-1
-	shop	donateshop2	-1,909:-1
-	shop	donateshop3	-1,909:-1
-	shop	donateshop4	-1,909:-1
-	shop	donateshop5	-1,909:-1
